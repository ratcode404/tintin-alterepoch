#NOP USE FOLLOWING SETTINGS IN WINDOW - COLUMNS 120 ROWS 62 - FONT SIZE 10

#SESSION {AE} {ae-mud.com} {9002}

#NOP ----------------------------------------------------------------------------------------
#NOP INITIATE
#NOP ----------------------------------------------------------------------------------------

#SPLIT 15 1 0 0 2
#FUNC NOW {#format result {%t} {%H:%M}}
#SCREEN SCROLLBAR ON 
#CONFIG {COMMAND ECHO} OFF

#NOP ----------------------------------------------------------------------------------------
#NOP LOAD FILES
#NOP ----------------------------------------------------------------------------------------

#READ highlight.tin
#READ macros.tin

#NOP ----------------------------------------------------------------------------------------
#NOP CHATBOX
#NOP ----------------------------------------------------------------------------------------

#VARIABLE {window}
{
     {1} {}
     {2} {}
     {3} {}
     {4} {}
     {5} {}
     {6} {}
     {7} {}
     {8} {}
     {9} {}
     {10} {}
     {11} {}
}
#ALIAS {showtowin}
{
     #list window del 1;
     #list window ins -1 {%0};
     #DRAW WHITE BOX 1 1 13 120 $window[-11..-1]
}

#NOP ----------------------------------------------------------------------------------------
#NOP RPXP BOX AND SUPRESS RPXP IN MAIN
#NOP ----------------------------------------------------------------------------------------

#VAR {mv_table}
{
	{fresh} 		{<afa>Fresh(10/10)}
	{rested} 		{<cfa>Rested(9/10)}
	{worked} 		{<dfa>Worked(8/10)}
	{tired} 		{<efa>Tired(7/10)}
	{weary} 		{<ffa>Weary(6/10)}
	{fatigued} 		{<fea>Fatigued(5/10)}
	{drained} 		{<fda>Drained(4/10)}
	{sapped} 		{<fba>Sapped(3/10)}
	{enervated} 		{<faa>Enervated(2/10)}
	{exhausted} 		{<eaa>Exhausted(1/10)}
	{unconscious}		{<eaa>Unconscious(0/10)}
}

#VAR {hp_table}
{
	{perfect} 		{<afa>Perfect(10/10)}
	{excellent} 		{<cfa>Excellent(9/10)}
	{scratched} 		{<dfa>Scratched(8/10)}
	{bruised} 		{<efa>Bruised(7/10)}
	{battered} 		{<ffa>Battered(6/10)}
	{hurt} 			{<fea>Hurt(5/10)}
	{wounded} 		{<fda>Wounded(4/10)}
	{injured} 		{<fba>Injured(3/10)}
	{mauled} 		{<faa>Mauled(2/10)}
	{mangled} 		{<eaa>Mangled(1/10)}
	{crippled}		{<eaa>Crippled(0/10)}
	{dying}			{<eaa>Dying(X/10)}
}

#ALIAS {empty_line_handler}
{
	#regex {%0} {[RPXP Gain: %* MV:%S %*} {#var mv_table[current] $mv_table[&2]};
	#regex {%0} {[RPXP Gain: %* HP:%S %*} {#var hp_table[current] $hp_table[&2]};
	#switch {{%0}}
	{
		#case {{[RPXP Gain: %* MV:%S (whererp) (hooded)%*}}
		{
			#DRAW WHITE BOX 13 1 15 120 <ccd>@NOW{} <bbc>%1 %2 %3 [$hp_table[current]<bbc>] [$mv_table[current]<bbc>] [ <fab>HOODED <bbc>] [ <fab>WHERE<bbc> ] %7%8;#line gag
		};
		#case {{[RPXP Gain: %* MV:%S %* (hooded)%*}}
		{
			#DRAW WHITE BOX 13 1 15 120 <ccd>@NOW{} <bbc>%1 %2 %3 [$hp_table[current]<bbc>] [$mv_table[current]<bbc>] [ <fab>HOODED <bbc>] [ <g02>where<bbc> ] %6%7;#line gag
		};
		#case {{[RPXP Gain: %* MV:%S (whererp) %*}}
		{
			#DRAW WHITE BOX 13 1 15 120 <ccd>@NOW{} <bbc>%1 %2 %3 [$hp_table[current]<bbc>] [$mv_table[current]<bbc>] [ <g02>hooded <bbc>] [ <fab>WHERE<bbc> ] %6%7;#line gag
		};
		#case {{[RPXP Gain: %* MV:%S %*}}
		{
			#DRAW WHITE BOX 13 1 15 120 <ccd>@NOW{} <bbc>%1 %2 %3 [$hp_table[current]<bbc>] [$mv_table[current]<bbc>] [ <g02>hooded <bbc>] [ <g02>where<bbc> ] %6%7;#line gag
		};
		#case {{[OOC] %* %*}}
		{
		    #regex {%0} {[OOC] %* %*} {showtowin <ccd>@NOW{} <bfe>[OOC] <bbe>&1<fff> &2;#line gag}
		};
		#case {{(visnet) %* %*}}
		{
		    #regex {%0} {(visnet) %* %*} {showtowin <ccd>@NOW{} <bfb>(visnet) <bbc>&1<fff> &2;#line gag}
		};
		#case {{(%* tells you %* %*}}
		{
		    #regex {%0} {%* tells you %*} {showtowin <ccd>@NOW{} <ffa>&1 tells you &2;#line gag}
		};
		#default
		{
		    #line ignore #showme
		}
    }
}

#ACTION {^$}
{
    #line gag;
    #line oneshot #ACTION {%*} {empty_line_handler %%0}
}
{4}

#NOP ----------------------------------------------------------------------------------------
#NOP LOGGING
#NOP ----------------------------------------------------------------------------------------

#CONFIG LOG PLAIN
#LOG OVERWRITE log.txt

#NOP ----------------------------------------------------------------------------------------
#NOP QUICK CONFIG MACROS
#NOP ----------------------------------------------------------------------------------------

#NOP TODO

#NOP ----------------------------------------------------------------------------------------
#NOP LOADED
#NOP ----------------------------------------------------------------------------------------
